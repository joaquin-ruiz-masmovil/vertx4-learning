plugins {
  id 'java'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '6.1.0'
  id "jacoco"
  id "com.avast.gradle.docker-compose" version '0.14.0'
}

group = "com.masmovil"
version = "1.0.0-SNAPSHOT"
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
  mavenCentral()
}

configurations {
  integrationtestImplementation.extendsFrom testImplementation
  integrationtestTestRuntimeClasspath.extendsFrom testRuntimeClasspath
}

sourceSets {
  integrationtest {
    compileClasspath += main.output + test.output
    runtimeClasspath += main.output + test.output
  }
}

ext {
  vertxVersion = "4.0.2"
  micronautVersion = "2.3.3"
  junitJupiterVersion = "5.7.0"
  mockitoVersion = "3.8.0"

  mainVerticleName = "com.masmovil.phoneapp.infrastructure.verticle.RestfulVerticle"
  launcherClassName = "io.vertx.core.Launcher"
}

application {
  mainClassName = launcherClassName
}

dependencies {
  implementation(platform("io.vertx:vertx-stack-depchain:$vertxVersion"))
  implementation "io.vertx:vertx-web-client"
  implementation "io.vertx:vertx-rx-java2"
  implementation "io.vertx:vertx-web"
  implementation "io.vertx:vertx-json-schema"

  // Vert.x Config
  compile "io.vertx:vertx-config"
  compile "io.vertx:vertx-config-yaml"

  // Vertx Postgres JDBC
  implementation "io.vertx:vertx-pg-client"

  // IoC
  implementation "io.micronaut:micronaut-inject-java:$micronautVersion"
  annotationProcessor "io.micronaut:micronaut-inject-java:$micronautVersion"

  // String Utils
  implementation "org.apache.commons:commons-lang3:3.11"

  // Preconditions
  implementation "com.google.guava:guava:23.0"

  // DataBase Migration
  implementation("org.flywaydb:flyway-core:5.2.4")
  implementation("org.postgresql:postgresql:42.2.12")

  // -- TEST --
  testImplementation "io.vertx:vertx-junit5"
  testImplementation "org.junit.jupiter:junit-jupiter:$junitJupiterVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
  testImplementation "org.mockito:mockito-core:$mockitoVersion"
  testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"

}

task integrationtest(type: Test) {

  testClassesDirs = sourceSets.integrationtest.output.classesDirs
  classpath = sourceSets.integrationtest.runtimeClasspath

  doFirst {
    dockerCompose.exposeAsEnvironment(integrationtest)
    dockerCompose.exposeAsSystemProperties(integrationtest)
  }
  mustRunAfter(test)

}

check.dependsOn(integrationtest)

dockerCompose.isRequiredBy(integrationtest)

dockerCompose {
  useComposeFiles = ['docker-compose.yml'] // like 'docker-compose -f <file>'
  captureContainersOutput = true // prints output of all containers to Gradle output - very useful for debugging
  stopContainers = true // doesn't call `docker-compose down` - useful for debugging
  removeContainers = true
  removeOrphans = true
  upAdditionalArgs = ['--force-recreate']
}

shadowJar {
  archiveClassifier.set("fat")
  manifest {
    attributes 'Main-Verticle': mainVerticleName
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << '-Xlint:unchecked'
  options.deprecation = true
}

test {
  useJUnitPlatform()
}

integrationtest {
  useJUnitPlatform()
}

tasks.withType(Test) {
  reports.html.destination = file("${reporting.baseDir}/${name}")

  testLogging {
    // set options for log level LIFECYCLE
    events "skipped", "failed", "standardOut"
    showExceptions true
    exceptionFormat "short"
    showCauses true
    showStackTraces true

    info.events = debug.events
    info.exceptionFormat = debug.exceptionFormat

    afterSuite { desc, result ->
      if (!desc.parent) {
        def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
        def startItem = '|  ', endItem = '  |'
        def repeatLength = startItem.length() + output.length() + endItem.length()
        println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
      }
    }
  }
}



run {
  args = ['run', mainVerticleName,
          "--launcher-class=$launcherClassName",
          "--redeploy=src/**/*.*",
          "--on-redeploy=./gradlew classes"
  ]
}
